# Maintainer: Sven Kiljan <sven dot archlinuxarm dash pbp at kiljan dot org>
# Contributor: Dan Johansen <strit@manjaro.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

# ALARM: Kevin Mihelich <kevin@archlinuxarm.org>
#  - enable omap, exynos, tegra, etnaviv for v7
#  - drop valgrind makdepend

pkgname=libdrm-pinebookpro
_pkgname=libdrm
pkgver=2.4.107
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
url="https://dri.freedesktop.org/"
arch=('x86_64' 'aarch64')
license=('custom')
depends=('libpciaccess')
makedepends=('python-docutils' 'meson')
checkdepends=('cairo')
replaces=('libdrm-new' 'libdrm-nouveau')
conflicts=($_pkgname)
provides=($_pkgname)
source=("https://dri.freedesktop.org/$_pkgname/$_pkgname-$pkgver.tar.xz"
        "0001-Prevent-dirty-tracking-for-Pinebook-Pro.patch"
        "COPYING")
sha512sums=('c7542ba15c4c934519a6a1f3cb1ec21effa820a805a030d0175313bb1cc796cd311f39596ead883f9f251679d701e262894c5a297d5cf45093c80a6cd818def0'
            '106cc357421f1cfb07e8f27800d6121c9919a544a6511c1bd7577950400793d39216989d5b4c37f1449110315eef0e42ebf04af861eb25b20d7fa2938e3f1301'
            '03dd85ad0f54b84b84a794b89de4e2d2c7b87f09934a09957c6b585a6101fcf3d46bd4889ed83dea6c36704541a84c4e77d659e12d77a4324fd2936e88f4ac11')
            
prepare() {
    cd $_pkgname-$pkgver
    patch -Np1 -i ../0001-Prevent-dirty-tracking-for-Pinebook-Pro.patch
}

build() {
  [[ "$CARCH" == "armv7h" ]] && CONFIG="-Domap=true -Dexynos=true -Dtegra=true -Detnaviv=true"
  arch-meson $_pkgname-$pkgver build \
    -Dudev=false \
    -Dvalgrind=false \
    $CONFIG
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  install -Dt "$pkgdir/usr/share/licenses/$_pkgname" -m644 COPYING
} 
